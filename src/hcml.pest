WHITESPACE = _{
    " "
  | "\t"
  | NEWLINE
  | comment
}

COMMENT = { html_comment }

// Tags
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "-" | "_")* }

name    = @{ ident }
id      = @{ ident }
class   = @{ ident }
s_id    = _{ "#" ~ id }
s_class = _{ "." ~ class }

attribute  = ${ ident ~ ("=" ~ string)? }
attributes = _{ "(" ~ (attribute ~ ","?)* ~ ")" }

tag             =  {
    ((name ~ s_id? | s_id | s_class) ~ (s_class)* | special_tag) ~ attributes?
}
special_tag     =  { "!" ~ (ident | conditional_tag) }
conditional_tag = @{ "[" ~ ^"if" ~ (!"]" ~ ANY)* ~ "]" }

// Elements and blocks
element = { tag ~ (node | void) }
node      = { element | string | lit_string | block }

block =  { "{" ~ node_list ~ "}" }
void  = _{ ";" }

node_list = _{ node* }

// Strings
string     = ${ "\"" ~ inner ~ "\"" }
inner      = ${ (!"\"" ~ ANY)* }
lit_string = ${ PUSH("#"*) ~ "\"" ~ lit_inner ~ "\"" ~ POP }
lit_inner  = ${ (!("\"" ~ PEEK) ~ ANY)* }

// Comments
lc_text  = @{ (!"\n" ~ ANY)* }
bc_text  = @{ (!("*/" | "-*/") ~ ANY)* }
hlc_text = ${ (!"\n" ~ ANY)* }
hbc_text = ${ (!("*/" | "-*/") ~ ANY)* }
// Not "-" character
comment_ws = @{ !("\n" | "-") }
// WS without newline for comments
ws = @{ (" " | "\t")* }

// Completely ignored
comment = @{ line_comment | block_comment }
line_comment  = @{ "//" ~ comment_ws ~ lc_text }
block_comment = @{ "/*" ~ comment_ws ~ bc_text ~ comment_ws ~ "*/" }

// Translated into HTML comments
html_comment = @{ html_line_comment | html_block_comment }
html_line_comment  = @{ "//-" ~ ws ~ hlc_text }
html_block_comment = @{ "/*-" ~ ws ~ hbc_text ~ ws ~ "-*/" }

// Document
document = _{ SOI ~ node_list ~ EOI }

